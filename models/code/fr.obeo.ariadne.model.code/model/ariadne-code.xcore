@Ecore(nsURI="http://www.obeo.fr/dsl/Ariadne/Code", nsPrefix="ariadne-code")
@GenModel(editDirectory="/fr.obeo.ariadne.model.code.edit/src-gen")
@GenModel(editorDirectory="/fr.obeo.ariadne.model.code.editor/src-gen")
@GenModel(fileExtensions="ariadnecode")
@GenModel(copyrightText="Copyright (c) 2012 Obeo.
All rights reserved. This program and the accompanying materials
are made available under the terms of the Eclipse Public License v1.0
which accompanies this distribution, and is available at
http://www.eclipse.org/legal/epl-v10.html

Contributors:
    Stephane Begaudeau (Obeo) - initial API and implementation")
package fr.obeo.ariadne.model.code

import fr.obeo.ariadne.model.core.Entry
import fr.obeo.ariadne.model.core.VersionedElement

class Component extends VersionedElement {
	String identifier
	contains ClasspathEntry[] classpathEntries opposite component
	contains ResourcesContainer[] resourcesContainers
	contains Component[] subcomponents
	contains ReferencedService[] referencedServices
	contains ProvidedService[] providedServices
}

class ResourcesContainer extends VersionedElement {
	String path
	contains Resource[] resources
	contains ResourcesContainer[] subResourcesContainers
}

abstract class Resource extends VersionedElement {
}

class GenericResource extends Resource {
	ResourceKind resourceKind
}

enum ResourceKind {
	MANIFEST
	LICENSE
	COPYRIGHT
	ICONS
	VIDEO
	MEDIA
	DOCUMENTATION
	INTERNATIONALIZATION
	USER_INTERFACE
	SERVICE
	SETTINGS
	CONFIGURATION
	TEMPLATE
	MODEL
	BUILD
	LIBRARY
}

class ClasspathEntry {
	ClasspathEntryKind kind
	String inputFolder
	String outputFolder
	contains TypesContainer[] typesContainers
	container Component component opposite classpathEntries
}

enum ClasspathEntryKind {
	SOURCE
	CONTAINER
	OUTPUT
}

class TypesContainer extends VersionedElement {
	contains Type[] types
	contains TypesContainer[] typesContainers
}

abstract class Type extends VersionedElement {
	String qualifiedName
	VisibilityKind visibility
	contains Type[] internalTypes
	refers Annotation[] annotations
}

class PrimitiveType extends Type {
	
}

class Classifier extends Type {
	ClassifierKind kind
	boolean static
	boolean final
	boolean immutable
	boolean ^abstract
	refers Type[] superTypes
	refers Type[] typeParameters
	contains Constructor[] constructors
	contains Field[] fields
	contains Operation[] operations opposite classifier
}

enum ClassifierKind {
	CLASS
	INTERFACE
	OBJECT
	TRAIT
	ENUMERATION
}

class Field extends VersionedElement {
	String qualifiedName
	VisibilityKind visibility
	boolean static
	boolean ^abstract
	boolean final
	boolean immutable
	boolean ^transient
	refers Type[] types
	refers Annotation[] annotations
	refers derived Field[] overriddenFields
}

class Constructor extends VersionedElement {
	String qualifiedName
	VisibilityKind visibility
	boolean static
	boolean ^abstract
	boolean final
	boolean immutable
	boolean ^transient
	contains Parameter[] parameters
	refers Type[] types
	refers Annotation[] annotations
	refers derived Constructor[] overriddenConstructors
}

class Operation extends VersionedElement {
	String qualifiedName
	VisibilityKind visibility
	boolean static
	boolean ^abstract
	boolean final
	boolean immutable
	boolean ^transient
	contains Parameter[] parameters opposite operation
	container Classifier classifier opposite operations
	refers Type[] returnTypes
	refers Type[] types
	refers Annotation[] annotations
	refers derived Operation[] overriddenOperations
}

class Parameter extends VersionedElement {
	String qualifiedName
	VisibilityKind visibility
	boolean final
	boolean immutable
	refers Type[] types
	refers Annotation[] annotations
	container Operation operation opposite parameters
}

class Annotation extends Type {
	contains AnnotationField[] annotationFields
}

class AnnotationField extends VersionedElement {
	String defaultValue
	String qualifiedName
}

enum VisibilityKind {
	PUBLIC
	PROTECTED
	PACKAGE
	PRIVATE
}

class ReferencedService extends VersionedElement {
	String identifier
	refers ProvidedService[] providedServices
}

class ProvidedService extends VersionedElement {
	String identifier
	refers ReferencedService referencedService
}

class Environment extends VersionedElement {
	contains Entry[] entries
}